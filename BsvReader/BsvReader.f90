program bsvreader
  ! Read a Binary Simulated Values (BSV) file generated by mf6
  use ConstantsModule, only: LENOBSNAME
  use OpenSpecModule, only: access, form, action
  use InputOutputModule, only: urword
  use readers, only: readsngl, readcont, lenobsnamebsv
  implicit none
  integer :: icol, ierr=0, iin=7, iout=8, iprecision, istart, istat, &
             istop, nc, n
  double precision :: r
  character(len=4) :: obstype, cvar
  character(len=100) :: text1 = '', word
  character(len=200) :: filname = '', outfile
  logical :: lex
  ! -- formats
10 format('Output has been written to file: ',a,/)
20 format('Using input file: ',a)   
  !
  ! -- Get input BSV file name
  nc = command_argument_count()
  if (nc==0) then
    write(*,*)'Enter BSV file name: '
    read(*,'(a)',err=900)filname
  else
    call get_command_argument(1,filname,status=istat)
  endif
  inquire(file=filname,exist=lex)
  if (.not. lex) then
    write(*,*)'Error: Input file not found.'
    goto 900
  endif
  !
  ! -- Open the binary input file for reading
  open(iin,file=filname,access=access,form=form,status='OLD',action=action(1),err=900)
  write(*,20)trim(filname)
  !
  ! -- Open output file
  outfile = trim(filname)//'.csv'
  open(iout,file=outfile)
  !
  ! -- read the first 100 bytes and get observation type and precision
  read(iin,err=900,end=900)text1
  icol = 1
  call urword(text1,icol,istart,istop,0,n,r,iout,iin)
  obstype = text1(istart:istop)
  if (obstype .ne. 'sngl' .and. obstype .ne. 'cont') then
    write(*,*)'Error: invalid observation type.'
    goto 900
  endif
  call urword(text1,icol,istart,istop,0,n,r,iout,iin)
  word = text1(istart:istop)
  if (word=='single') then
    iprecision = 1
  elseif (word=='double') then
    iprecision = 2
  else
    write(*,*)'Error: invalid precision.'
    goto 900
  endif
  !
  ! Get LENOBSNAME from bytes 12-15 (allow field to be blank)
  cvar = text1(12:15)
  if (cvar == '') then
    ! Use default
    lenobsnamebsv = LENOBSNAME
  else
    read(cvar,'(i4)',iostat=istat)lenobsnamebsv
    if (istat /= 0) then
      write(*,*)'Error reading LENOBSNAME from header.'
      goto 900
    elseif (lenobsnamebsv < 1) then
      write(*,*)'Error: LENOBSNAME value read from header is invalid.'
      goto 900
    endif
  endif
  !
  ! -- read remaining data for either single or continuous observations
  if (obstype=='sngl') then
    call readsngl(iin,iout,iprecision,ierr)
  elseif (obstype=='cont') then
    call readcont(iin,iout,iprecision,ierr)
  endif
  if (ierr.ne.0) goto 900
  write(*,10)trim(outfile)
  !
  goto 1000
900 continue
  write(*,*)'Error encountered.'
  write(*,*)''
1000 continue
  close(iin)
  close(iout)
  stop
end program bsvreader
  
  
